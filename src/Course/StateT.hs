{-# LANGUAGE NoImplicitPrelude #-}
{-# LANGUAGE ScopedTypeVariables #-}
{-# LANGUAGE InstanceSigs #-}
{-# LANGUAGE RebindableSyntax #-}
{-# LANGUAGE OverloadedStrings #-}

module Course.StateT where

import Course.Core
import Course.Id
import Course.Optional
import Course.List
import Course.Functor
import Course.Apply
import Course.Applicative
import Course.Bind
import Course.Monad
import Course.State
import qualified Data.Set as S
import qualified Prelude as P

-- $setup
-- >>> import Test.QuickCheck
-- >>> import qualified Prelude as P(fmap)
-- >>> instance Arbitrary a => Arbitrary (List a) where arbitrary = P.fmap listh arbitrary

-- | A `StateT` is a function from a state value `s` to a functor f of (a produced value `a`, and a resulting state `s`).

newtype StateT s f a =
  StateT {
    runStateT :: s -> f (a, s)
  }

-- | Implement the `Functor` instance for @StateT s f@ given a @Functor f@.
--
-- >>> runStateT ((+1) <$> (pure 2) :: StateT Int List Int) 0
-- [(3,0)]

instance Functor f => Functor (StateT s f) where
  (<$>) :: (a -> b) -> StateT s f a -> StateT s f b
  (<$>) f (StateT aa) = StateT (\s -> mapFst f <$> aa s)


-- | Implement the `Apply` instance for @StateT s f@ given a @Bind f@.
--
-- >>> runStateT (pure (+2) <*> ((pure 2) :: StateT Int List Int)) 0
-- [(4,0)]
--
-- >>> import qualified Prelude as P
-- >>> runStateT (StateT (\s -> Full ((+2), s P.++ [1])) <*> (StateT (\s -> Full (2, s P.++ [2])))) [0]
-- Full (4,[0,1,2])
--
-- >>> runStateT (StateT (\s -> ((+2), s P.++ [1]) :. ((+3), s P.++ [1]) :. Nil) <*> (StateT (\s -> (2, s P.++ [2]) :. Nil))) [0]
-- [(4,[0,1,2]),(5,[0,1,2])]

instance Bind f => Apply (StateT s f) where
  (<*>) :: StateT s f (a -> b) -> StateT s f a -> StateT s f b
  (<*>) (StateT ff) (StateT aa) = StateT ((\(f, s) -> mapFst f <$> aa s) <=< ff)


-- | Implement the `Applicative` instance for @StateT s f@ given a @Applicative f@.
--
-- >>> runStateT (pure 2) 0
-- (2,0)
--
-- >>> runStateT ((pure 2) :: StateT Int List Int) 0
-- [(2,0)]

instance Monad f => Applicative (StateT s f) where
  pure :: a -> StateT s f a
  pure a = StateT (\s -> pure (a, s))


-- | Implement the `Bind` instance for @StateT s f@ given a @Monad f@.
-- Make sure the state value is passed through in `bind`.
--
-- >>> runStateT ((const $ putT 2) =<< putT 1) 0
-- ((),2)

instance Monad f => Bind (StateT s f) where
  (=<<) :: (a -> StateT s f b) -> StateT s f a -> StateT s f b
  (=<<) f (StateT aa) = StateT bb
    where bb s0 = aa s0 >>= (\(a, s1) -> runStateT (f a) s1)


instance Monad f => Monad (StateT s f) where


-- | Run the `StateT` seeded with `s` and retrieve the resulting state.

execT :: Functor f => StateT s f a -> s -> f s
execT (StateT a) s = snd <$> (a s)


-- | Run the `StateT` seeded with `s` and retrieve the resulting value.

evalT :: Functor f => StateT s f a -> s -> f a
evalT (StateT a) s = fst <$> (a s)


-- | A `StateT` where the state also distributes into the produced value.
--
-- >>> (runStateT (getT :: StateT Int List Int) 3)
-- [(3,3)]

getT :: Monad f => StateT s f s
getT = StateT (\s -> pure (s, s))


-- | A `StateT` where the resulting state is seeded with the given value.
--
-- >>> runStateT (putT 2) 0
-- ((),2)
--
-- >>> runStateT (putT 2 :: StateT Int List ()) 0
-- [((),2)]

putT :: Monad f => s -> StateT s f ()
putT x = StateT (\s -> pure ((), x))



-- | A `State'` is `StateT` specialised to the `Id` functor.
type State' s a =
  StateT s Id a

-- | Provide a constructor for `State'` values
--
-- >>> runStateT (state' $ runState $ put 1) 0
-- Id ((),1)

state' :: (s -> (a, s)) -> State' s a
state' f = StateT (Id . f)


-- | Provide an unwrapper for `State'` values.
--
-- >>> runState' (state' $ runState $ put 1) 0
-- ((),1)

runState' :: State' s a -> s -> (a, s)
runState' (StateT f) = runId . f


-- | Run the `State` seeded with `s` and retrieve the resulting state.

exec' :: State' s a -> s -> s
exec' ff = runId . execT ff


-- | Run the `State` seeded with `s` and retrieve the resulting value.

eval' :: State' s a -> s -> a
eval' ff = runId . evalT ff


-- | Remove all duplicate elements in a `List`.
--
-- /Tip:/ Use `filtering` and `State'` with a @Data.Set#Set@.
--
-- prop> distinct' xs == distinct' (flatMap (\x -> x :. x :. Nil) xs)

distinct' :: (Ord a, Num a) => List a -> List a
distinct' as =  eval' (filtering p as) S.empty
  where p = \a -> state' (S.notMember a &&& S.insert a)
-- not done


-- | Remove all duplicate elements in a `List`.
-- However, if you see a value greater than `100` in the list,
-- abort the computation by producing `Empty`.
--
-- /Tip:/ Use `filtering` and `StateT` over `Optional` with a @Data.Set#Set@.
--
-- >>> distinctF $ listh [1,2,3,2,1]
-- Full [1,2,3]
--
-- >>> distinctF $ listh [1,2,3,2,1,101]
-- Empty

distinctF :: (Ord a, Num a) => List a -> Optional (List a)
distinctF as = evalT (filtering p as) S.empty
  where p = \a -> StateT (t a)
        t = \a s -> if a > 100 then Empty else Full (S.notMember a s, S.insert a s)


-- | An `OptionalT` is a functor of an `Optional` value.

data OptionalT f a =
  OptionalT {
    runOptionalT :: f (Optional a)
  }

-- | Implement the `Functor` instance for `OptionalT f` given a Functor f.
--
-- >>> runOptionalT $ (+1) <$> OptionalT (Full 1 :. Empty :. Nil)
-- [Full 2,Empty]

instance Functor f => Functor (OptionalT f) where
  (<$>) f (OptionalT aa) = OptionalT ((f <$>) <$> aa)


-- | Implement the `Apply` instance for `OptionalT f` given a Apply f.
--
-- >>> runOptionalT $ OptionalT (Full (+1) :. Full (+2) :. Nil) <*> OptionalT (Full 1 :. Empty :. Nil)
-- [Full 2,Empty,Full 3,Empty]

instance Apply f => Apply (OptionalT f) where
  (<*>) (OptionalT ff) (OptionalT aa) = OptionalT (lift2 (<*>) ff aa)


-- | Implement the `Applicative` instance for `OptionalT f` given a Applicative f.

instance Applicative f => Applicative (OptionalT f) where
  pure aa = OptionalT ((pure . pure) aa)


-- | Implement the `Bind` instance for `OptionalT f` given a Monad f.
--
-- >>> runOptionalT $ (\a -> OptionalT (Full (a+1) :. Full (a+2) :. Nil)) =<< OptionalT (Full 1 :. Empty :. Nil)
-- [Full 2,Full 3,Empty]

instance Monad f => Bind (OptionalT f) where
  (=<<) g (OptionalT aa) = OptionalT (f =<< aa)
    where f o = case o of
                  Empty  -> pure Empty
                  Full a -> runOptionalT (g a)


instance Monad f => Monad (OptionalT f) where



-- | A `Logger` is a pair of a list of log values (`[l]`) and an arbitrary value (`a`).
data Logger l a =
  Logger (List l) a
  deriving (Eq, Show)

-- | Implement the `Functor` instance for `Logger
--
-- >>> (+3) <$> Logger (listh [1,2]) 3
-- Logger [1,2] 6
instance Functor (Logger l) where
  (<$>) f (Logger l a) = Logger l (f a)


-- | Implement the `Apply` instance for `Logger`.
--
-- >>> Logger (listh [1,2]) (+7) <*> Logger (listh [3,4]) 3
-- Logger [1,2,3,4] 10
instance Apply (Logger l) where
  (<*>) (Logger k f) (Logger l a) = Logger (k ++ l) (f a)


-- | Implement the `Applicative` instance for `Logger`.
--
-- >>> pure "table" :: Logger Int P.String
-- Logger [] "table"
instance Applicative (Logger l) where
  pure a = Logger Nil a


-- | Implement the `Bind` instance for `Logger`.
-- The `bind` implementation must append log values to maintain associativity.
--
-- >>> (\a -> Logger (listh [4,5]) (a+3)) =<< Logger (listh [1,2]) 3
-- Logger [1,2,4,5] 6
instance Bind (Logger l) where
  (=<<) f (Logger l a) = let Logger k b = f a in Logger (l ++ k) b


instance Monad (Logger l) where



-- | A utility function for producing a `Logger` with one log value.
--
-- >>> log1 1 2
-- Logger [1] 2

log1 :: l -> a -> Logger l a
log1 l = Logger (l :. Nil)


-- | Remove all duplicate integers from a list. Produce a log as you go.
-- If there is an element above 100, then abort the entire computation and produce no result.
-- However, always keep a log. If you abort the computation, produce a log with the value,
-- "aborting > 100: " followed by the value that caused it.
-- If you see an even number, produce a log message, "even number: " followed by the even number.
-- Other numbers produce no log message.
--
-- /Tip:/ Use `filtering` and `StateT` over (`OptionalT` over `Logger` with a @Data.Set#Set@).
--
-- >>> distinctG $ listh [1,2,3,2,6]
-- Logger ["even number: 2","even number: 2","even number: 6"] (Full [1,2,3,6])
--
-- >>> distinctG $ listh [1,2,3,2,6,106]
-- Logger ["even number: 2","even number: 2","even number: 6","aborting > 100: 106"] Empty

distinctG :: (Integral a, Show a) => List a -> Logger Chars (Optional (List a))
distinctG as = runOptionalT $ evalT (filtering p as) S.empty
  where p = \a -> StateT $ \s -> if a > 100 then (abort a s) else (check a s)
        abort = \a _ -> OptionalT $ log1 (listh "aborting > 100: " ++ show' a) Empty -- '
        check = \a s -> OptionalT $ logx a (Full (S.notMember a s, S.insert a s))
        logx  = \a o -> if even a then log1 (listh "even number: " ++ show' a) o else Logger Nil o

{-
-- t :: a -> S.Set a -> Optional (Bool, S.Set a)
-- p :: a -> StateT (S.Set a) Optional Bool
-- filtering :: (a -> f Bool) -> List a -> f (List a) // f = StateT (S.Set a) Optional

distinctF :: (Ord a, Num a) => List a -> Optional (List a)
distinctF as = evalT (filtering p as) S.empty
  where p = \a -> StateT (t a)
        t = \a s -> if a > 100 then Empty else Full (S.notMember a s, S.insert a s)
-}
